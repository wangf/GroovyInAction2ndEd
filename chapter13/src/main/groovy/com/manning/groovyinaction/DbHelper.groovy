package com.manning.groovyinaction

import groovy.sql.Sql
import groovy.text.SimpleTemplateEngine as STE

class DbHelper {
    Sql db

    DbHelper() {
        def source = new org.h2.jdbcx.JdbcDataSource()
        source.URL = 'jdbc:h2:mem:GinA;DB_CLOSE_DELAY=-1'
        source.user = 'sa'
        source.password = ''
        db = new Sql(source)
    }

    def simpleTemplate = new STE().createTemplate('''
DROP   INDEX ${lowname}Idx IF EXISTS;
DROP   TABLE $name    IF EXISTS;
CREATE TABLE $name (
    ${lowname}Id    INTEGER GENERATED BY DEFAULT AS IDENTITY,
$fields
);
CREATE INDEX ${lowname}Idx ON $name (${lowname}Id);''')

    def executeDdl(DataAccessObject dao) {
        def template = simpleTemplate
        def binding = [
                name: dao.tablename,
                lowname: dao.tablename.toLowerCase(),
                fields: dao.schema.collect { key, val ->
                    "    ${key.padRight(12)} $val"
                }.join(",\n")
        ]
        def stmt = template.make(binding).toString()
        db.execute stmt
    }
}
